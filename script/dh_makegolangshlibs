#!/usr/bin/perl -w

=head1 NAME

dh_makegolangshlibs - Generates golang-specific shlibs files and substvars

=cut

use strict;
use Debian::Debhelper::Dh_Lib;
use File::Find;
use JSON;
use MIME::Base64;

=head1 SYNOPSIS

B<dh_makegolangshlibs> [S<I<debhelper options>>]

=head1 DESCRIPTION

B<dh_makegolangshlibs> is a debhelper program which generates a
substvar and shlibs file to enable packages depending on go shared
libraries to conveniently depend on the ABI hash of the library rather
than just the soversion.  It replaces dh_makeshlibs in the usual dh
sequences.

=head1 NOTES

The best way to invoke B<dh_makegolangshlibs> is by using B<dh --with=golang>.

=cut

init();

############################################################################
# Generate ...
############################################################################

my ($libpkg, $devpkg);

foreach my $pkg (@{$dh{DOPACKAGES}}) {
    if ($pkg =~ /^lib.*[0-9]$/) {
        $libpkg = $pkg;
    }
    if ($pkg =~ /-dev$/) {
        $devpkg = $pkg;
    }
}

if (!defined($libpkg) || !defined($devpkg)) {
    printf("could not find lib and dev packages");
    exit(1);
}

my $devpkgdir = tmpdir($devpkg);
my @jsonfiles;
find({
    wanted => sub {
        my $F = $File::Find::name;

        if ($F =~ /json$/ ) {
            push @jsonfiles, $F;
        }
    },
    no_chdir => 1,
     } ,$devpkgdir);
if (@jsonfiles != 1) { die "too many / too few"; }
open( my $jh, '<', @jsonfiles ) or die "wtf @jsonfiles";
my $json_text   = <$jh>;
close($jh);
my $shlibdata = decode_json($json_text);
if (!defined($shlibdata->{Hash})) {
    printf("malformed shlibdata: Hash not found");
    exit(1);
}
my $hexhash;
foreach (split //, decode_base64($shlibdata->{Hash})) {
    $hexhash .= sprintf("%02lx", ord($_));
}

my $libpkgdir=tmpdir($libpkg);
my ($libname, $sover) = ($libpkg =~ /^lib(.*[^0-9])-?([0-9]+)$/);

if (! -d "$libpkgdir/DEBIAN") {
    doit("install","-d","$libpkgdir/DEBIAN");
}
my $provides = "$libpkg-$hexhash";
addsubstvar($libpkg, "golang:Provides", $provides);
complex_doit("echo 'lib$libname $sover $provides' >>$libpkgdir/DEBIAN/shlibs");
autoscript($libpkg,"postinst","postinst-makeshlibs");
autoscript($libpkg,"postrm","postrm-makeshlibs");


=head1 SEE ALSO

dh(1)

=head1 AUTHORS

Michael Hudson-Doyle <michael.hudson@canonical.com>

=cut

# vim:ts=4:sw=4:et
